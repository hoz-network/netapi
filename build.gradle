plugins {
    id 'java'
    id 'org.screamingsandals.plugin-builder' version '1.0.59'
}

allprojects {
    group 'net.hoz'
    version '1.1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.screamingsandals.plugin-builder'

    repositories {
        mavenCentral()
        maven {
            url = "https://repo.screamingsandals.org/public/"
        }

        if (System.getenv("hoz_internal_token") != null) {
            maven {
                url = "https://repo.hoznet.dev/internal"
                name = "hoz-internal"

                credentials(HttpHeaderCredentials) {
                    name = 'Private-Token'
                    value = System.getenv("hoz_internal_token")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }

    configurations.all {
        // Check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    dependencies {
        api group: 'com.google.guava', name: 'guava', version: GUAVA_VERSION
        api group: 'org.slf4j', name: 'slf4j-api', version: SLF4J_VERSION
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }
}